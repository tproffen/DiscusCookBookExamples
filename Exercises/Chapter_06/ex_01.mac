discus
############################################################
# EXERCISE 1
#
# (A) Introducing both waves SEPARATELY using the 'wave'
#     segment of DISCUS
#
############################################################
# First we read (as in the other examples) the content
# of the unit cell from the file 'cell.cll' and expand
# it to a model crystal of 100x100x1 unit cells. In our
# case we have only a ZR atom on (0,0,0). Have a look at
# the file 'cell.cll'.
############################################################
#
read
cell cell.cll,100,100,1
#
############################################################
# First, we simply run two different density waves through
# the crystal. Most of the commands are similar to the
# example given in section 2. We choose one wave vector
# as <100> with a wave length of 50A and the second wave
# vector as <110> with a wave length of 35A. Our wave
# simply replaces zirconium atoms by vacancies with a 
# probability corresponding to the current amplitude of the
# density wave.
#
# The probability of removing a Zr for each of the two 
# waves shall only oscillate between 0.0 and 0.5. Note
# that the corresponding 'plow' and 'phigh' commands give
# the probability of NOT removing a Zr, thus the values
# are 0.5 and 1.0.
############################################################
#
wave
  dens
  func sinus
  phase 0.0
  plow 0.5
  phigh 1.0
  dese all
  repl zr,void
#
  len 35.0
  vect 1,1,0
  run
#
  len 50.0
  vect 1,0,0
  run
#
exit
#
############################################################
# The macros 'four' and 'plot' used for all simulations
# in this chapter calculate the diffraction pattern and
# export the created structure for plotting. Calculating
# the Fourier transform can take a couple of minutes 
# depending on the computer the simulation is run on.
############################################################
#
@four ex_01a
@plot ex_01a
#
############################################################
#
# (B) Introducing both waves simultaneously using the 
#     FORTRAN style interpreter of DISCUS
#
############################################################
# Reading the starting structure again ..
############################################################
#
read
cell cell.cll,100,100,1
#
############################################################
# Now we introduce the two density waves 'manually'. The
# DO loop goes over all atoms within the crystal. Variables
# r[1] and r[2] are assigned the argument for the two COS
# terms. Note that the wave vectors are normalised and the
# wave lenght needs to be specified in unit cell units 
# (a=5A). The resulting amplitude of the wave is assigned
# to variable r[3] which then determines the probability
# for the Zr atom to be removed.
#
# For details about variables and the FORTRAN style inter-
# preter of DISCUS, refer to the DISCUS Users Guide.
############################################################
#
r[8]=1./sqrt(2.)
r[9]=2.*3.141592654
#
do i[1]=1,n[1]
  r[1]=r[9]*x[i[1]]/10.0
  r[2]=r[9]*(x[i[1]]*r[8]+y[i[1]]*r[8])/7.0
  r[3]=0.5+0.25*cos(r[1])+0.25*cos(r[2])
  if (r[3].gt.ran(0)) then
    m[i[1]]=0
  endif
enddo
#
############################################################
# Calculate and save Fourier transform and crystal ..
############################################################
#
@four ex_01b
@plot ex_01b
#
exit
############################################################
# Plotting
############################################################
@kex_01
wait return
